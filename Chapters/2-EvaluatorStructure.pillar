!! A First Interpreter: A Simple Evaluator

In this chapter we will develop step by step a Pharo program interprerter.
We will incrementally add support for language characteristics.
Here is the progression: returning constants, accessing receiver state, instance variable assignment, simple temporaries, self messages, ....



For each of the steps we will start by defining a method illustrating the situation, then we define a test and 
we implement the features. 

Doing so we will define and refine a visitor. 
Note that we will write the visitor from scratch but we will reuse the node of the Pharo AST and their functionalities.

!!! Setting the Stage

The tests that we will define are hosted in the class ==CHInterpreterTest==.

[[[
TestCase subclass: #CHInterpreterTest
	instanceVariableNames: 'interpreter'
	classVariableNames: ''
	package: 'Champollion-Core'
]]]

We define two helper methods: ==interpreter== and ==interpreterClass== as follows: 

[[[
CHInterpreterTest >> interpreter [
	^ interpreter
]
]]]


[[[
CHInterpreterTest >> interpreterClass [
	^ CHInterpreter
]
]]]


[[[
CHInterpreterTest >> setUp [
	super setUp.
	interpreter := self interpreterClass new
]
]]]

!!! Hosting Code of be Tested
We prefer to separate the code that we will use to test the behavior of our interpreter from the test class itself. It will give us more possibility to describe different situations in the future.

The class ==CHInterpretable== is a class hosting the sample methods that we will execute. 


[[[
Object subclass: #CHInterpretable
	instanceVariableNames: ''
	classVariableNames: ''
	package: 'Champollion'
]]]

Here we define some simple methods that we would like to execute.
For now we focus on returning simple literal objects such as booleans, float and integer. 

[[[
CHInterpretable >> returnInteger [
	^ 5
]
]]]

[[[
CHInterpretable >> returnFloat [
	^ 3.14
]
]]]

[[[
CHInterpretable >> returnBoolean [
	^ false
]
]]]


!!! A First Test

The method ==testReturnInteger== is the first test for specifying the behavior of simple interpreter. 

[[[
CHInterpreterTest >> testReturnInteger
	| ast result |
	ast := (Interpretable >> #returnInteger) ast.
	result := self interpreter execute: ast. 
	self assert: result equals: 5
]]]

Our first test is worth one comment: 
- we access the AST of the method that we want to execute
- then we instantiate our interpreter and 
- ask it to execute the ast.
And we check that we get the expected result. 

!!! Building a Minimal Constant Evaluator

We are ready to define our interpreter. 
We define the class ==CHInterpreter==. 
We will implement it as a Visitor over the Pharo method AST. 

[[[
Object subclass: #CHInterpreter
	instanceVariableNames: ''
	classVariableNames: ''
	package: 'Champollion'
]]]

Note that we do not inherit from the default Pharo AST Visitor so that we build it step by step. 
Nevertheless we follow the same API and use the node ==accept:== visit methods.
The method ==execute:== simply calls the visitor main entry point (method ==visitNode:==).

[[[
CHInterpreter >> execute: anAST [
	^ self visitNode: anAST
]
]]]

[[[
CHInterpreter >> visitNode: aNode [
	^ aNode acceptVisitor: self
]
]]]

Since the AST we will interpret is an AST of method, 
The first visitor method of interest is the method ==visitMethodNode:==.
It just dispatches on ==visitMethodBodyNode:==.

[[[
CHInterpreter >> visitMethodNode: aMethodNode [
	^ self visitMethodBodyNode: aMethodNode body
]
]]]

For now ==visitMethodBodyNode:== just visits the method body (an ==RBSequenceNode==).

[[[
CHInterpreter >> visitMethodBodyNode: aMethodBodyNode [
	^ self visitNode: aMethodBodyNode
]
]]]

The first version of ==visitSequenceNode:== ignores possible temporary declarations and handles all the statements paying attention that the last statement value should be returned.
So we visit all the statements except the last one, and we return the value of the last statement. 

[[[
CHInterpreter >> visitSequenceNode: aSequenceNode [
	aSequenceNode statements allButLast 
		do: [ :each | self visitNode: each ].
	^ self visitNode: aSequenceNode statements last
]	
]]]

Since we want to be able to access a return value, we define the ==visitReturnNode:== to handle. This method simply returns the value (invoking recursively the visitor) of the expression that is returned.

[[[
CHInterpreter >> visitReturnNode: aReturnNode [
	^ self visitNode: aReturnNode value
]
]]]

For now we only manage literal object such as number, booleans, ...
So we redefine the method ==visitLiteralValueNode:== which is invoked on literal values. 

[[[
CHInterpreter >> visitLiteralValueNode: aRBLiteralValueNode [
	^ aRBLiteralValueNode value
]
]]]

Our first test passes. 
And we are ready to continue our journey.



!!! Improving the Test Infrastructure

Since we will write many tests during this book, we exact some logic from our test method, it will help future evolution. 

We define the method ==executeSelector:==, it accesses the method, gets its AST and returns the value of the  execution of the ast.
[[[
CHInterpreterTest >> executeSelector: aSymbol [
	| ast |
	ast := (CHInterpretable >> aSymbol) ast.
	^ self interpreter executeMethod: ast
]
]]]

Now we rewrite our first test as follows: 

[[[
CHInterpreterTest >> testReturnInteger [

	self 
		assert: (self executeSelection: #returnInteger)
		equals: 5
]
]]]

We are ready to efficiently write tests for the other constants. 

!!! More Tests for Constant

It is straigth forward to write new tests. 
We define a test for booleans, here we do not use ==deny:== to make the result explicit.
Note that we include the method definition ofthe interpreted method to ease understanding. 

[[[
CHInterpreterTest >> testReturnBoolean [
	"returnBoolean
		^ false"
		
	self assert: (self executeSelector: #returnBoolean) equals: false
]
]]]

[[[
CHInterpreterTest >> testReturnFloat [
	"returnFloat
		^ 3.14"
	self assert: (self executeSelector: #returnFloat) equals: 3.14
]
]]]


!!! Literal Array

We add support for Array. 
First we define some method returning an empty array and a literal array. 

[[[
CHInterpretable >> returnEmptyArray [
	^ #()
]
]]]

[[[
CHInterpretable >> returnLiteralArray [
	^ #(true 1 'ahah')
]
]]]

We add a test to cover case.


[[[
CHInterpreterTest >> testReturnEmptyArray [
	self 
		assert: (self executeSelector: #returnEmptyArray) 
		equals: #()
]
]]]


[[[
CHInterpreterTest >> testReturnLiteralArray [
	self 
		assert: (self executeSelector: #returnLiteralArray) 
		equals: #(true 1 'ahah')
]
]]]

We now have to implement the method ==visitLiteralArrayNode:== to handle arrays.
Here we return the values returned by the interpretation of the elements. 

[[[
CHInterpreter >> visitLiteralArrayNode: aRBLiteralArrayNode [
	^ aRBLiteralArrayNode contents 
			collect: [ :each | self visitNode: each ]
]
]]]

The tests are green.
Up until now we did not consider that any form of variable and we should handle such case.


!!! Handling Self 
The first kind of variable that we handle is ==self== that represents the receiver of the message. 
THe first things we need to do is to make sure that we can specify the receiver of a message


Since we need to have access to the receiver for now we add an instance variable called ==receiver==.
This is a super naive way to handle it since we will need a way to automatically manage receiver and arguments.

[[[
Object subclass: #CHInterpreter
	instanceVariableNames: 'receiver'
	classVariableNames: ''
	package: 'Champollion'
]]]

We define an accessor so that we can easily adapt when we will introduce a better way representation later. 

[[[
CHInterpreter >> receiver [
	^ receiver
]
]]]

We make sure that we can set the receiver when we launch the interpretation and we introduce a new method ==execute:with:== for this. 
We will remove 

[[[
CHInterpreter >> execute: anAST withReceiver: anObject [
	receiver := anObject.
	^ self visitNode: anAST
]
]]]

We redefine the method ==executeSelector:== to use this new method.

[[[
CHInterpreterTest >> executeSelector: aSymbol [
	| ast |
	ast := (CHInterpretable >> aSymbol) ast.
	^ self interpreter executeMethod: ast withReceiver: nil
]
]]]

This way all our tests are still passing and we will be ready to handle the case of ==self== and ==super==.


[[[
CHInterpretable >> returnSelf [
	^ self
]
]]]

[[[
CHInterpreterTest >> testReturnSelf [
	self 
		assert: (self executeSelector: #returnSelf) 
		equals: nil
]
]]]

The interpretation of ==self== is done in the method ==visitSelfNode:==.
It is simple, it just returns the value of the receiver stored in the interpreter.

[[[
CHInterpretable >> visitSelfNode: aRBSelfNode [
	^ self receiver
]
]]]

!!! Handling Super
Following the same logic as for ==self==, we improve the interpreter to support ==super==.

We start by defining a method using ==super== and its companion test. 

[[[
CHInterpretable >> returnSuper [
	^ super
]
]]]

[[[
CHInterpreterTest >> testReturnSuper [
	self 
		assert: (self executeSelector: #returnSuper) 
		equals: nil
]
]]]

What is interesting is that the interpretation of ==super== is that this variable is also the receiver of the message. 
So contrary to a common and wrong believe, ==super== is not the superclass or an instance of the superclass. It is just the receiver.

[[[
CHInterpreter >> visitSuperNode: aRBSelfNode [
	^ self receiver
]
]]]

The logic of the tests is not really satisfactory, so we improve it. 

!!! Improving the Logic of setUp

We want to have a better way to handle receiver in test.
We add an instance variable to the ==CHInterpreterTest== that will hold the receiver.

[[[
TestCase subclass: #CHInterpreterTest
	instanceVariableNames: 'interpreter receiver'
	classVariableNames: ''
	package: 'Champollion'
]]]

In the ==setUp== we assign a new instance to the new instance variable.

[[[
CHInterpreterTest >> setUp [
	super setUp.
	interpreter := self interpreterClass new.
	receiver := CHInterpretable new
]
]]]

We then use the new instance variable in ==executeSelector:==.

[[[
CHInterpreterTest >> executeSelector: aSymbol [
	| ast |
	ast := (CHInterpretable >> aSymbol) ast.
	^ self interpreter executeMethod: ast withReceiver: receiver
]
]]]

We rewrite the test methods ==testReturnSelf== and ==testReturnSuper==.
[[[
CHInterpreterTest >> testReturnSelf [
	self 
		assert: (self executeSelector: #returnSelf) 
		equals: receiver
]
]]]

[[[
CHInterpreterTest >> testReturnSuper [
	self 
		assert: (self executeSelector: #returnSuper) 
		equals: receiver
]
]]]

We are ready to work on more interesting points. 



!!! Receiver State Access

We can work on support instance variable access. 
Let us start with a test.

!!!!! Fixture and Test

We define a getter and setter to be able to control the value put in the instance variable of our instance.

[[[
CHInterpretable >> returnX [
	^ x
]
]]]
 
[[[
CHInterpretable >> x: anInteger [
	x := anInteger
]
]]]

We modify the receiver and when we interpret the accessor we should get the correct value.

[[[
CHInterpreterTest >> testReturnAccessInstanceVariable [
	receiver x: 100.
	self assert: (self executeSelector: #returnX) equals: 100
]
]]]


Our test is failing so we are ready to work.  And now we can define the method ==visitInstanceVariableNode:== to reflectivity access the state of receiver. Note that this implementation does not take into account the fact that an instance variable can be defined in a superclass and still be accessible in the subclass method. 

[[[
CHInterpreter >> visitInstanceVariableNode: aVariableNode [
	^ receiver instVarNamed: aVariableNode name
]	
]]]

Our test should pass!

!!! Receiver State Assignment
We will add support for assignment in receiver's instance variables.

!!!!! Fixture and Tests

We define a method doing an assignment into the instance variable ==x==.
[[[
CHInterpretable >> store100IntoX [
	x := 100
]	
]]]

[[[
CHInterpretable >> x [
	^ x
]
]]]

The test ==testStore100IntoX== launches the interpretation of method ==store100IntoX==.

[[[
CHInterpreterTest >> testStore100IntoX [
	receiver x: 17.
	self executeSelector: #store100IntoX.
	self assert: receiver x equals: 100
]
]]]

!!!!! Implementation

The implementation is straighforward. We define the method ==visitAssignmentNode:==.
Here since we expect to have other variable types in the future we define the storing behavior  (method == store:with:==) of the assigned variable node itself. 

[[[
CHInterpreter >> visitAssignmentNode: anAssignmentNode [
	" x := 100"
	
	| value | 
	value := (self visitNode: anAssignmentNode value).
	anAssignmentNode variable store: value with: self.
	^ value
]
]]]


[[[
RBInstanceVariableNode >> store: anObject with: anInterpreter [
	anInterpreter receiver instVarNamed: name put: anObject
]
]]]

Now our tests should pass. 


!!! Handling Global Variables
We finish this chapter with the handling of global variables such as classes and globals. 
We decided not to use the default Pharo environment so that we have to be more explicit about how to access globals.
It also means that we can control the scope of our interpreter and setting different environments.

We first define the method ==returnGlobal== which accesses and returns the global named ==Global==.

[[[
CHInterpretable >> returnGlobal [
	^ Global
]
]]]

We define a test which specifies that the interpreter' environment has a binding whose key is #Global and value is a new object.

[[[
CHInterpreterTest >> testReturnGlobal [
	| globalObject |
	globalObject := Object new.
	interpreter globalEnvironmentAt: #Global put: globalObject. 
	self assert: (self executeSelector: #returnGlobal) equals: globalObject
]
]]]

We introduce a new instance variable named ==gloabEnvironment== and we initialize it to a new dictionary.

[[[
Object subclass: #CHInterpreter
	instanceVariableNames: 'stack globalEnvironment'
	classVariableNames: ''
	category: 'Champollion-Core'
]]]

[[[
CHInterpreter >> initialize [
	super initialize. 
	stack := CTStack new.
	globalEnvironment := Dictionary new
]
]]]

We add ==globalEnvironmentAt:put:== to add a new binding to the environment.
[[[
CHInterpreter >> globalEnvironmentAt: aSymbol put: anObject [
	globalEnvironment at: aSymbol put: anObject
]
]]]

Finally we simply define a new method ==visitGlobalNode:== that will return the value of a global node
by returning the value associated with the key in the global environment.

[[[
CHInterpreter >> visitGlobalNode: aRBGlobalNode [
	^ self globalEnvironment at: aRBGlobalNode name 
		ifAbsent: [ self error: aRBGlobalNode name, ' not found' ]
]
]]]


!!! Conclusion
This interpreter is basic. It does not cover arguments, temporaries or messages. 
This is what we will cover in the following chapter.
Then we will cover blocks and their specific semantics in subsequent chapters.

