!! AST Visitors

The cornerstone of an AST interpreter are the ASTs, short for abstract syntax tree.
An abstract syntax tree is a tree data structure that represents a program from the syntax point of view. In other words, each node in the tree will represent an element that is written in a program. To illustrate it, consider the piece of Pharo code below:

[[[language=smalltalk
variable := 'constant' , self message
]]]

This piece of code assigns into a variable named ==variable== the result of sending the ==,== message to a =='constant'== string, with ==self message== as argument. An AST will then organize the code above as a tree containing nodes representing variables, assignments, strings, and message sends.

In the first part of this chapter we will study ASTs by looking at the existing AST implementation in Pharo, the RBAST. RBAST is the AST implementation used currently (Pharo 9.0) by many tools in Pharo's tool-chain, such as the compiler, the syntax-highlighter, the auto-completion, the code quality engine and the refactoring engine. As so, it's an interesting piece of engineering, and we will find it provides most of what we will need for our journey to have fun with interpreters.

In the second part of the chapter we will study (or re-study, for those who already know it) the visitor design pattern. To be usable by the many tools named before, RBASTs implement a visitor interface. Tools performing complex operations on ASTs may then visitor classes with their algorithm. As we will see in the chapters after this one, one such tool is an interpreter, thus mastering ASTs and visitors is essential.

!!! Pharo Abstract Syntax Trees

An abstract syntax tree is a tree data structure that represents a program from the syntax point of view. In the tree, nodes represent the syntactic elements of the program.
The edges in the tree represent how those nodes are related.

The Pharo standard distribution comes with a pretty complete AST implementation that is used by many tools. To get our hands over an AST, we could build it ourselves manually, or as we will do in this chapter, we could ask a parser to parse some text and build an AST for us. Fortunately, Pharo also includes a parser that does exactly this: the ==RBParser==.
The ==RBParser== class implementas a parser for the Pharo syntax.
It has two main modes of working: parsing expressions and parsing methods.

!!!! Parsing Expressions

Expressions are constructs that can be evaluated to a value.
For example, the program ==17 max: 42== is the message send ==max:== to receiver 17 with argument 42, and can be evaluated to the value 42 (since it is bigger than 17).

[[[language=smalltalk
expression := RBParser parseExpression: '17 max: 42'.

expression receiver formattedCode
>>> 17.

expression selector
>>> #max.

expression arguments first formattedCode
>>> 42.
]]]

Expressions can be combined to build more complex expressions. In the following example, the expresion =17 max: 42= is used as the receiver of another message expression, the message ==#asString== with no arguments.

[[[language=smalltalk
expression := RBParser parseExpression: '(17 max: 42) asString'.

expression receiver formattedCode
>>> (17 max: 42).

expression selector
>>> #asString.

expression arguments
>>> #()
]]]

Of course, message sends are not the only expressions we have in Pharo.
Another kind of expression that appeared already in the examples above are literal objects such as numbers.

[[[language=smalltalk
expression := RBParser parseExpression: '17'.

expression formattedCode
>>> 17.
]]]

!!!! The Literal Node

Literal nodes represent literal objects. A literal object is an object that is not created directly by sending the ==new== message to a class. Instead, the developer writes directly in the source code the value of that object, and the object is created automatically from it. Literal objects in Pharo are strings, numbers, characters, booleans, nil and literal arrays.

Literal nodes in Pharo are instances of the ==RBLiteralValueNode==, and understand the message ==#value== which returns the value of the object. In other words, literal objects in Pharo are resolved at parse time. Notice that the ==#value== message does not return a string representation of the literal object, but the literal object itself.

[[[language=smalltalk
integerExpression := RBParser parseExpression: '17'.
integerExpression value
>>> 17.

trueExpression := RBParser parseExpression: 'true'.
trueExpression value
>>> true.

"Remember, strings need to be escaped"
stringExpression := RBParser parseExpression: '''a string'''.
stringExpression value
>>> 'a string'.
]]]

A special case of literals are literal arrays, which have their own node: ==RBLiteralArrayNode==.
Literal array nodes understand the message ==#value== as any other literal, returning the literal array instance. However, it allows us to access the sub collection of literals using the message ==#contents==. 

[[[language=smalltalk
arrayExpression := RBParser parseExpression: '#(1 2 3)'.
arrayExpression value
>>> #(1 2 3).

arrayExpression contents first
>>> RBLiteralValueNode(1).
]]]

!!!! The Variable Node, self and super nodes

Variable nodes in the AST tree are used when variables are used or assigned to.
Variables are instances of ==RBVariableNode== and know their ==#name==.

[[[language=smalltalk
variableExpression := RBParser parseExpression: 'aVariable'.
variableExpression name
>>> 'aVariable'.
]]]

Variable nodes are used to equally denote temporary, argument, instance, class or global variables.
That is because at parse-time, the parser cannot always differentiate when a variable is of one kind or another.
This is specially true when we talk about instance, class and global variables, because the context to distinguish them has not been made available.
Instead of complexifying the parser with this kind of information, the Pharo toolchain does it in a pipelined fashion, leaving those tools using the AST to decide how to do it.
Then the parser generates a simple AST, and tools can later annotate that AST with semantic information from a context if required.
An example of this kind of treatment is the compiler, which requires such contextual information to produce the correct final code.
For the matter of this booklet, we will not consider nor use semantic analysis, and we will stick with normal ==RBVariableNode== objects.

!!!! Assignment nodes

Assignment nodes in the AST represent assignment expressions using the ==:\=== operator.
Assignments are indeed expressions: their value is the value of the variable after the assignment.
We will see in the next chapter, when implementing an evaluator, why this is important.

An assignment node is an instance of ==RBAssignmentNode==.
If we send it the ==variable== message answers the variable it assigns to.
The message ==value== returns the expression at the right of the assignment.

[[[language=smalltalk
assignmentExpression := RBParser parseExpression: 'var := #( 1 2 ) size'.
assignmentExpression variable
>>> RBVariableNode(var).

assignmentExpression value
>>> RBMessageNode(#(1 2) size).
]]]

!!!! Message nodes

Message nodes are the core of Pharo programs, and they are the most common composed expression node we will find in the AST.
Message are instances of ==RBMessageNode== and they have a receiver, a selector and a collection of arguments, obtained through the ==#receiver==, ==#selector== and ==#arguments== messages.
We say that message nodes are composed expressions because the ==#receiver== and ==#arguments== of a message are expressions in themselves, which can be as simple as literals or variables, or it can be other composed messages too.

[[[language=smalltalk
messageExpression := RBParser parseExpression: '17 max: 42'.
messageExpression receiver
>>> RBLiteralValueNode(17).
]]]

Note that the arguments is a normal collection of expressions.

[[[language=smalltalk
messageExpression arguments
>>> an OrderedCollection(RBLiteralValueNode(42)).
]]]

And that the selector is also just a symbol.

[[[language=smalltalk
messageExpression selector
>>> #max:.
]]]

!!!! A note on message nodes and precedence

For those readers that already master the syntax of Pharo, you will remember that there exist three kind of messages: unary, binary and keyword messages. Besides their number of parameters, the Pharo syntax accords an order of precedences between them too. I.e., unary messages get to be evaluated before binary messages, which get to be evaluated before keyword messages. Only parentheses override this precedence.
Precedence of messages in RBASTs is resolved at parse-time. In other words, the output of ==RBParser== is an AST respecting the precedence rules of Pharo.

Let's consider a couple of examples illustrating this.
If we feed the ==RBParser== with the expression below, it will create a ==RBMessageNode== as we already know it.
The root of that message node is they ==keyword:== message, and its first argument is the ==argument + 42 unaryMessage== subexpression.
That subexpression is in turn another message node with the ==+== binary selector, whose first argument is the ==42 unaryMessage== subexpression.

[[[
variable keyword: argument + 42 unaryMessage
]]]

Now, let's change the expression adding extra parenthesis as in:

[[[
variable keyword: (argument + 42) unaryMessage
]]]

The resulting AST completely changed! The root is still the ==keyword:== message, but now its first argument is the ==unaryMessage== sent to a (now in parenthesis) ==(argument + 42)== receiver.

Finally, if modify the parenthesis again to wrap the keyword message, the root of the resulting AST has changed too. It is now the ==+== binary message.

[[[
(variable keyword: argument) + 42 unaryMessage
]]]

==RBParser== can be a nice tool to try and play with Pharo expressions and master precedence!

!!!! Cascade nodes

Cascade nodes represent cascaded message expressions.
Cascaded messages are messages separated by semi-colons (==;==) such as in the following example.

[[[language=smalltalk
OrderedCollection new
    add: 17;
    add: 42;
    yourself
]]]

Which is, in practical terms, equivalent to a sequence of messages to the same receiver:

[[[language=smalltalk
t := OrderedCollection new.
t add: 17.
t add: 42.
t yourself
]]]


However, in contrast with the sequence above, cascades are expressions: their value is the value of the last message in the cascade.

A cascade node is an instance of ==RBCascadeNode==.
A cascade node understands the ==receiver== message, returning the receiver of the cascade.
It also understands the ==messages== message, returning a collection with the messages in the cascade.
Note that the messages inside the cascade node are normal ==RBMessageNode== and have a receiver too.
They indeed share the same receiver than the cascade.
In the following chapters we will have to be careful when manipulating cascade nodes, to avoid to wrongly manipulate twice the same receiver.

[[[language=smalltalk
cascadeExpression := RBParser parseExpression: 'var msg1; msg2'.
cascadeExpression receiver
>>> RBVariableNode(var).

cascadeExpression messages
>>> an OrderedCollection(RBMessageNode(var msg1) RBMessageNode(var msg2))
]]]

!!!! Dynamic Literal Array nodes

Pharo has dynamic literal arrays. Dynamic literal arrays differ from literal arrays in that its elements are calculated at runtime instead of at parse time. To delay the execution of the elements in the dynamic array, a dynamic array node contains expressions, separated by dots.

[[[language=smalltalk
{ 1 + 1 . self message . anObject doSomethingWith: anArgument + 3 }
]]]

Dynamic literal arrays nodes are instances of ==RBArrayNode==.
To access the expressions inside an dynamic array node, they understand the message ==children==

[[[language=smalltalk
arrayNode := RBParser parseExpression: '{ 
  1 + 1 . 
  self message . 
  anObject doSomethingWith: anArgument + 3 }'.

arrayNode children.
>>> an OrderedCollection(
  RBMessageNode((1 + 1))
  RBMessageNode(self message)
  RBMessageNode((anObject doSomethingWith: anArgument + 3)))
]]]

!!! Method and Block nodes

Now that we have studied the basic nodes representing expressions, we can build up methods from them.
Methods in RBAST are represented as instances of ==RBMethodNode== and need to be parsed with a variant of the parser we have used so far, a method parser. The ==RBParser== class fulfills the role of a method parser when we use the message ==parseMethod:== instead of ==parseExpression:==. For example, the following piece of code returns a ==RBMethodNode== instance for a method named ==myMethod==.

[[[language=smalltalk
methodNode := RBParser parseMethod: 'myMethod
	1+1.
	self'
]]]

A method node differs from the expression nodes we have seen before in the fact that method nodes can only be roots in the AST tree. Method nodes cannot be children of other nodes.

Method nodes have a name or selector, accessed through the ==selector== message, a list of arguments, accessed through the ==arguments== message, and as we will see in the next section they also contain a ""body"" with the list of statements in the method.

[[[language=smalltalk
methodNode selector
>>> #myMethod
]]]

!!!! Sequence nodes

Method nodes have a body, represented as a ==RBSequenceNode==.
A sequence node is a sequence of instructions or statements.
All expressions are statements, including all nodes we have already seen such as literals, variables, arrays, assignments and message sends.
We will introduce later two more instructions that can be included as part of a sequence node: block nodes and return nodes.
Block nodes are expressions that are syntactically and thus structurally similar to methods.
Return nodes, representing the return instruction ==^==, are instruction nodes but not expression nodes, i.e., they can only be children of sequence nodes.

If we take the example from before, we can access the sequence node body of our method with the ==body== message.

[[[language=smalltalk
methodNode := RBParser parseMethod: 'myMethod
	1+1.
	self'.

methodNode body.
>>> RBSequenceNode(1 + 1. self)
]]]

And we can access and iterate the instructions in the sequence by asking it its ==statements==.

[[[language=smalltalk
methodNode body statements.
>>> an OrderedCollection(RBMessageNode(1 + 1) RBSelfNode(self))
]]]

Besides the instructions, sequence nodes also are the ones defining temporary variables.
Consider for example the following method defining a temporary.
In RBAST, temporary variables are defined as part of the sequence node and not the method node.
This is because temporary variables can be defined inside a block node, as we will see later.
We can access the temporary variables of a sequence node by asking it for its ==temporaries==.

[[[language=smalltalk
myMethod
  | temporary |
	1+1.
	self'.
]]]

[[[language=smalltalk
methodNode := RBParser parseMethod: 'myMethod
  | temporary |
  1+1.
  self'.
methodNode body temporaries.
>>> an OrderedCollection(RBVariableNode(temporary))
]]]

!!!! Return nodes

Return AST nodes represent the instruction using the caret character ==^==.
Return nodes, instances of ==RBReturnNode== are not expression nodes, i.e., they can only be found as a direct child of sequence nodes.
Return nodes do represent the fact of returning a value, and that value is an expression, which we can access through the ==value== message.

[[[language=smalltalk
methodNode := RBParser parseMethod: 'myMethod
  1+1.
  ^ self'.

returnNode := methodNode body statements last.
>>>RBReturnNode(^ self)

returnNode value.
>>>RBSelfNode(self)
]]]

Note that as in Pharo return statements are not mandatory in a method, they are not mandatory in the AST either.
Indeed, we can have method ASTs without return nodes.
In those cases, the semantics of Pharo specify that ==self== is implicitly returned.
It is interesting to note that the AST does not force semantics but only syntax: we will add semantics to the AST when we evaluate it in a subsequent chapter.

!!!! Block nodes

Block nodes represent block closures expressions.
A block closure is an object syntactically delimited by square brackets ==[]== that contains statements and can be evaluated using the ==value== message and its variants.
The block node is the syntactic counter-part of a block closure: it is expression that, when evaluated, will create the block object.

Block nodes work by most means like method nodes: they have a list of arguments and a sequence node as body containing temporaries and statements.
They differentiate from methods in two aspects: first, they do not have a selector, second, they are expressions (and thus can be parsed with ==parseExpression:==).

[[[
blockNode := RBParser parseExpression: '[ :arg | | temp | 1 + 1. temp ]'.

blockNode arguments.
>>>an OrderedCollection(RBVariableNode(arg))

blockNode body temporaries.
>>>an OrderedCollection(RBVariableNode(temp))

blockNode body statements.
>>>an OrderedCollection(RBMessageNode(1 + 1) RBVariableNode(temp))
]]]

!!! Basic ASTs Manipulations

We have already covered all of Pharo AST nodes, and how to access the information in them.
Those knowing ASTs for other languages, would have noticed that we have indeed few nodes.
This is because in Pharo, control-flow statements such as conditionals or loops are expressed as messages, so no special case for them is required in the syntax.
Because of this, we say that Pharo's syntax fits inside a post-card.

In this section we will explore some core-messages of RBAST, that allow common manipulation for all nodes: iterating the nodes, storing meta-data and testing methods.
Most of these manipulations are rather primitive and simple.
In the next section we will see how the visitor pattern in conjunction with ASTs empowers us, and gives us the possibility to build more complex applications such as concrete and abstract evaluators as we will see in the next chapters.

!!!! Iterating over an AST

ASTs are indeed trees, and we can traverse them as any other tree.
RBASTs provide several protocols for accessing and iterating any AST node in a generic way.

- ==aNode children==: returns a collection with the direct children of the node.
- ==aNode allChildren==: returns a collection with all recursive children found from the node.
- ==aNode nodesDo: aBlock==: iterates over allChildren applying ==aBlock== on each of them.
- ==aNode parent==: returns the direct parent of the node.
- ==aNode methodNode==: returns the method node that is the root of the tree. For consistency, expressions nodes parsed using ==parseExpression:== are contained within a method node too.

!!!! Storing Properties

Some manipulations require storing meta-data associated to AST nodes.
RBASTs provide a protocols for storing arbitrary properties inside a node.
Properties stored in a node are indexed by a key, following the API of Pharo dictionaries.

- ==aNode propertyAt: aKey put: anObject==: inserts ==anObject== at ==aKey==, overriding existing values at ==aKey==.
- ==aNode hasProperty: aKey==: returns a boolean indicting if the node contains a property indexed by ==aKey==.
- ==aNode propertyAt: aKey==: returns the value associated with ==aKey==. If ==aKey== is not found, fails with an exception.
- ==aNode propertyAt: aKey ifAbsent: aBlock==: returns the value associated with ==aKey==. If ==aKey== is not found, evaluates the block and returns the its value.
- ==aNode propertyAt: aKey ifAbsentPut: aBlock==: returns the value associated with ==aKey==. If ==aKey== is not found, evaluates the block, inserts the value of the block at ==aKey== and returns the value.
- ==aNode propertyAt: aKey ifPresent: aPresentBlock ifAbsent: anAbsentBlock==: Searches for the value associated with ==aKey==. If ==aKey== is found, evaluates ==aPresentBlock== with the its value. If ==aKey== is not found, evaluates the block and returns the its value.
- ==aNode removeProperty: aKey==: removes the property at ==aKey==. If ==aKey== is not found, fails with an exception.
- ==aNode removeProperty: aKey ifAbsent: aBlock==: removes the property at ==aKey==. If ==aKey== is not found, evaluates the block and returns the its value.


!!!! Testing Methods

RBASTs provide a testing protocol that can be useful for small applications and writing unit tests.
All RBASTs answer the messages ==isXXX== with a boolean ==true== or ==false==.
A first set of methods allow us to ask a node if it is of a specified type:

- ==isLiteralNode==
- ==isLiteralArray==
- ==isVariable==
- ==isAssignment==
- ==isMessage==
- ==isCascade==
- ==isDynamicArray==
- ==isMethod==
- ==isSequence==
- ==isReturn==

And we can also ask a node if it is an expression node or not:

- ==isValue==


!!! AST Visitors

In the previous sections we have seen how to create and manipulate AST nodes.
The ==RBParser== class implements a parser of expressions and methods that return AST nodes for the text given as argument.
With the AST manipulation methods we have seen before, we can already write queries on an AST.
For example, counting the number of message sends in an AST is as simple as the following loop.

[[[language=smalltalk
count := 0.
aNode nodesDo: [ :n | 
  n isMessage
    ifTrue: [ count := count + 1 ] ].
count
]]]

However, more complex manipulations do require more than an iteration and a conditional.
When an different operation is required for each kind of node in the AST, potentially with special cases depending on how nodes are composed, one object-oriented alternative is to implement it using the visitor design pattern.
In this section we start reviewing the visitor pattern, and we then apply it for a single task: searching a string inside the tree.

!!!! The Visitor Pattern

The visitor pattern is one of the original design patterns from Gamma et Al.
The main purpose of the visitor pattern is to externalize an operation from a data structure.
For example, let's consider a file system implemented with the composite pattern, where nodes can be files or directories.
This composite forms a tree, where file nodes are leaf nodes and directory nodes are non-leaf nodes.

[[[language=smalltalk
Object subclass: #FileNode
  instanceVariableNames: 'size'
  classVariableNames: ''
  package: 'VisitorExample'
  
Object subclass: #DirectoryNode
  instanceVariableNames: 'children'
  classVariableNames: ''
  package: 'VisitorExample'
]]]

Using this tree, we can take advantage of the composite pattern and the polymorphism between both nods to calculate the total size of a node implementing a polymorphic ==size== method in each class.

[[[language=smalltalk
FileNode >> size [
  ^ size
]

DirectoryNode >> size [
  ^ children sum: [ :each | each size ]
]
]]]

Now, let's consider the users of the file system library want to extend it with their own operations.
If the users have access to the classes, they may extend them just by adding methods to them.
However, chances are users do not have access to the library classes.
One way to open the library classes is to implement the visitor ""protocol"": each library class will implement a generic ==acceptVisitor: aVisitor==  method that will perform a re-dispatch on the argument giving information about the receiver.
For example, when a ==FileNode== receives the ==acceptVisitor:== message, it will send the argument the message ==visitFileNode:==, identifying itself as a file node.

[[[language=smalltalk
FileNode >> acceptVisitor: aVisitor [
  ^ aVisitor visitFileNode: self
]

DirectoryNode >> acceptVisitor: aVisitor [
  ^ aVisitor visitDirectoryNode: self
]
]]]

In this way, we can re-implement a ==SizeVisitor== that calculates the total size of a node in the file system as follows.
When a size visitor visits a file, it asks the file for its size.
When it visits a directory, it must iterate the children and sum the sizes.
However, it cannot directly ask the ==size== of the children, because only ==FileNode== instances do understand it but directories do not. Because of this, we need to make a recursive call and re-ask the child node to accept the visitor. Then each node will again dispath on the size visitor.


[[[language=smalltalk
SizeVisitor >> visitFileNode: aFileNode [
  ^ aFileNode size
]

SizeVisitor >> visitDirectoryNode: aDirectoryNode [
  ^ aDirectoryNode children sum: [ :each | each accept: self ]
]
]]]

As the file system library forms trees that we can manipulate with a visitor, ASTs do so too.
RBASTs are already extensible through visitors: its nodes implement ==acceptVisitor:== methods on each of the nodes.
This means we can introduce operations on the AST that were not foreseen by the original developers.
In such cases, it is up to us to implement a visitor object with the correct ==visitXXX:== methods.

!!!! Introducing AST visitors: measuring the depth of the tree

To introduce how to implement an AST visitor on RBASTs, let's implement a visitor that returns the max depth of the tree. That is, a tree with a single node has a depth of 1. A node with children node has a depth of 1 + the maximum depth amongst all its children.
Let's call that visitor ==DepthCalculatorVisitor==.

[[[language=smalltalk
Object subclass: #DepthCalculatorVisitor
  instanceVariableNames: ''
  classVariableNames: ''
  package: 'VisitorExample'
]]]

RBAST nodes implement already the visitor pattern.
They have an ==acceptVisitor:== method that will dispatch to the visitor with corresponding visit methods.
This means we can already use our visitor, which will break on a visit.

!!!!! Visiting message nodes

Let's start by calculating the depth of the expression ==1+1==.
This expression is made of a message node, and two literal nodes.

[[[
expression := RBParser parseExpression: '1+1'.
expression acceptVisitor: DepthCalculatorVisitor new.
>>> Exception! DepthCalculatorVisitor does not understand visitMessageNode:
]]]

If we execute the example above, we get a debugger because ==DepthCalculatorVisitor== does not understand ==visitMessageNode:==. We can then proceed to introduce that method in the debugger using the button ""create"" or by creating it in the browser.
We can implement the visit method as follows, by iterating the children to calculate the maximum depth amongst the children, and then adding 1 to it.

[[[
DepthCalculatorVisitor >> visitMessageNode: aRBMessageNode
  ^ 1 + (aRBMessageNode children
      inject: 0
      into: [ :max :node | max max: (node acceptVisitor: self) ])
]]]

!!!!! Visiting literal nodes

As soon as we restart the example, it will stop again with an exception again, but this time because our visitor does not know how to visit literal nodes.
We know that literal nodes have no children, so we can implement the visit method as just returning one.

[[[
DepthCalculatorVisitor >> visitLiteralValueNode: aRBLiteralValueNode
  ^ 1
]]]

!!!!! Calculating the depth of a method

A method node contains a set of statements.
Statements are either expressions or return statements.
The example that follows parses a method with two statements whose maximum depth is 3.
The first statement, as we have seen above, has a depth of 2.
The second statement, however, has depth of three, because the receiver of the ==+== message is a message itself.
The final depth of the method is then 5: 1 for the method node, 1 for the sequence node, and 3 for the statements.

[[[
method := RBParser parseMethod: 'method
  1+1.
  self factorial + 2'.
method acceptVisitor: DepthCalculatorVisitor new.
>>> Exception! DepthCalculatorVisitor does not understand visitMethodNode:
]]]

To calculate the above, we need to implement three other visiting methods: ==visitMethodNode:==, ==visitSequenceNode:== and ==visitSelfNode:==. Since for both kind of nodes we have to iterate over all children in the same way, let's implement these similarly to our ==visitMessageNode:==.

[[[
DepthCalculatorVisitor >> visitMethodNode: aRBMethodNode
  ^ 1 + (aRBMethodNode children
      inject: 0
      into: [ :max :node | max max: (node acceptVisitor: self) ])
      
DepthCalculatorVisitor >> visitSequenceNode: aRBSequenceNode
  ^ 1 + (aRBSequenceNode children
      inject: 0
      into: [ :max :node | max max: (node acceptVisitor: self) ])
]]]



!!!! Searching the AST for a Token




!!! Conclusion